# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=switch-libssh2
pkgver=1.11.1
pkgrel=1
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts"
url="https://github.com/libssh2/libssh2"
license=('BSD')
arch=('any')
options=(!strip libtool staticlibs)
depends=('switch-mbedtls' 'switch-zlib')
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
groups=('switch-portlibs')
source=("https://libssh2.org/download/libssh2-${pkgver}.tar.xz")
sha256sums=('9954cb54c4f548198a7cbebad248bdc87dd64bd26185708a294b2b50771e3769')

prepare() {
  cd "libssh2-${pkgver}"
  autoreconf -fi
}

build() {
  cd "libssh2-${pkgver}"

  source ${DEVKITPRO}/switchvars.sh

  ./configure --prefix=${PORTLIBS_PREFIX} \
	--disable-shared --enable-static \
	--host=aarch64-none-elf \
	--with-crypto=mbedtls \
	--disable-examples-build \
	--disable-tests-build \
  --disable-docker-tests \
  --disable-sshd-tests

  make
}

package() {
  cd "libssh2-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/opt/devkitpro/portlibs/switch/licenses/${pkgname}/LICENSE"
}