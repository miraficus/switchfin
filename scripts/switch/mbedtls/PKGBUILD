_realname=mbedtls
pkgname=switch-${_realname}
pkgver=3.6.2
pkgrel=1
pkgdesc='An open source, portable, easy to use, readable and flexible SSL library'
arch=('any')
url='https://tls.mbed.org'
license=('apache')
options=(!strip libtool staticlibs)
depends=(switch-zlib)
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
source=( "https://github.com/Mbed-TLS/mbedtls/releases/download/mbedtls-${pkgver}/mbedtls-${pkgver}.tar.bz2"
         "mbedtls-${pkgver}.patch" )
groups=('switch-portlibs')
sha256sums=('8b54fb9bcf4d5a7078028e0520acddefb7900b3e66fec7f7175bb5b7d85ccdca' 'SKIP')

build() {
  cd mbedtls-$pkgver

  patch -Np1 -i $srcdir/mbedtls-${pkgver}.patch

  source ${DEVKITPRO}/switchvars.sh

  aarch64-none-elf-cmake \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DUSE_SHARED_MBEDTLS_LIBRARY=OFF \
    -DUSE_STATIC_MBEDTLS_LIBRARY=ON \
    -DMBEDTLS_FATAL_WARNINGS=OFF \
    -DENABLE_PROGRAMS=OFF \
    -DENABLE_TESTING=OFF \
    .

  make -j$(nproc)

}

package() {

  cd mbedtls-$pkgver
  make install DESTDIR="$pkgdir"

}
