# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libssh2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-mbedtls"
pkgver=1.11.1
pkgrel=1
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts (mingw-w64)"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-${_realname}-wincng")
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'clangarm64')
url="https://libssh2.org/"
msys2_references=(
  'cpe: cpe:/a:libssh2:libssh2'
)
msys2_repository_url='https://github.com/libssh2/libssh2'
license=("spdx:BSD-3-Clause")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-autotools"
  "${MINGW_PACKAGE_PREFIX}-mbedtls"
  "${MINGW_PACKAGE_PREFIX}-zlib"
)
source=("https://libssh2.org/download/${_realname}-${pkgver}.tar.xz")
sha256sums=('9954cb54c4f548198a7cbebad248bdc87dd64bd26185708a294b2b50771e3769')

prepare() {
  cd ${_realname}-${pkgver}
  autoreconf -fi
}

build() {
  builddir="${srcdir}/build-${MSYSTEM}"
  mkdir -p "${builddir}" && cd "${builddir}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --disable-rpath \
    --disable-examples-build \
    --disable-tests-build \
    --disable-docker-tests \
    --disable-sshd-tests \
    --with-crypto=mbedtls
  make
}

package() {
  make -C "${srcdir}/build-${MSYSTEM}" DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/${_realname}-${pkgver}/COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}